/*
 * Copyright 2015-2018 Canoo Engineering AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.bmuschko.nexus'

def pomConfig = {
    name project.name
    url project.project_website
    inceptionYear '2015'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url project.project_scm
    }
    developers {
        [
            'Hendrik Ebbers': 'hendrik.ebbers@canoo.com',
            'Michael Heinrichs': 'michael.heinrichs@canoo.com'
        ].each { devName, devEmail ->
            developer {
                name devName
                email devEmail
                organization 'Canoo Engineering AG'
                organizationUrl 'http://www.canoo.com'
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

extraArchive {
    sources = false
    tests   = false
    javadoc = false
}

modifyPom {
    project(pomConfig)
}

if (!project.hasProperty('nexusReleaseDeployURL')) ext.nexusReleaseDeployURL = ''
if (!project.hasProperty('nexusSnapshotDeployURL')) ext.nexusSnapshotDeployURL = ''

nexus {
    sign = true
    repositoryUrl = nexusReleaseDeployURL
    snapshotRepositoryUrl = nexusSnapshotDeployURL
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().children().last() + pomConfig
                asNode().appendNode('description', project.project_description)
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Build-Revision': project.buildRevision,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
        )
    }
    metaInf {
        from(rootProject.files('.')) {
            include 'LICENSE'
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'rico'
        userOrg = 'karakun'
        name = 'rico'
        desc = project.project_description
        licenses = ['Apache-2.0']
        labels = ['']
        websiteUrl = project.project_website
        issueTrackerUrl = project.project_issues
        vcsUrl = project.project_scm
        publicDownloadNumbers = true
    }
}